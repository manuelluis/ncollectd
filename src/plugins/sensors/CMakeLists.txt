plugin_option(PLUGIN_SENSORS "sensors metrics" ON)

set(BUILD_PLUGIN_SENSORS 0)

if(PLUGIN_SENSORS)
    if(BUILD_DARWIN)
        find_package(LibIokit)
        if(LIBIOKIT_FOUND)
            set(BUILD_PLUGIN_SENSORS 1)
        else()
            set(BUILD_PLUGIN_SENSORS "libiokit not found" PARENT_SCOPE)
        endif()
    elseif(BUILD_LINUX)
        find_package(LibSensors)
        if (LIBSENSORS_FOUND)
            set(BUILD_PLUGIN_SENSORS 1)
        else()
            set(BUILD_PLUGIN_SENSORS_REASON "libsensors not found" PARENT_SCOPE)
        endif()
    else()
        set(BUILD_PLUGIN_SENSORS_REASON "unsupported system" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_SENSORS ${BUILD_PLUGIN_SENSORS} PARENT_SCOPE)

if(BUILD_PLUGIN_SENSORS)
    set(PLUGIN_SENSORS_SRC sensors.c)
    set(PLUGIN_SENSORS_LIBS libutils libmetric)

    if(BUILD_DARWIN)
        list(APPEND PLUGIN_SENSORS_SRC sensors_darwin.c)
        list(APPEND PLUGIN_SENSORS_LIBS LibIokit::LibIokit)
    elseif(BUILD_LINUX)
        list(APPEND PLUGIN_SENSORS_SRC sensors_linux.c)
        list(APPEND PLUGIN_SENSORS_LIBS LibSensors::LibSensors)
    endif()

    add_library(sensors MODULE ${PLUGIN_SENSORS_SRC})
    target_link_libraries(sensors PRIVATE ${PLUGIN_SENSORS_LIBS})
    set_target_properties(sensors PROPERTIES PREFIX "")

    install(TARGETS sensors DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-sensors.5 ncollectd-sensors.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-sensors.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
