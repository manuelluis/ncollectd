plugin_option(PLUGIN_USERS "User statistics" ON)

include(CheckIncludeFile)
include(CheckFunctionExists)

set(BUILD_PLUGIN_USERS 0)

if(PLUGIN_USERS)
    check_include_file(utmp.h HAVE_UTMP_H)
    check_include_file(utmpx.h HAVE_UTMPX_H)
    if(HAVE_UTMPX_H OR HAVE_UTMP_H)
        check_function_exists(getutent  HAVE_GETUTENT)
        check_function_exists(getutxent HAVE_GETUTXENT)
        if(HAVE_GETUTENT OR HAVE_GETUTXENT)
            set(BUILD_PLUGIN_USERS 1)
        else()
            set(BUILD_PLUGIN_USERS_REASON "getutent or getutxent not found" PARENT_SCOPE)
        endif()
    else()
        set(BUILD_PLUGIN_USERS_REASON "utmp.h or utmpx.h not found" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_USERS ${BUILD_PLUGIN_USERS} PARENT_SCOPE)

if(BUILD_PLUGIN_USERS)
    set(PLUGIN_USERS_SRC users.c)
    add_library(users MODULE ${PLUGIN_USERS_SRC})

    if(HAVE_UTMP_H)
        target_compile_definitions(users PUBLIC HAVE_UTMP_H)
    endif()
    if(HAVE_UTMPX_H)
        target_compile_definitions(users PUBLIC HAVE_UTMPX_H)
    endif()
    if(HAVE_GETUTENT)
        target_compile_definitions(users PUBLIC HAVE_GETUTENT)
    endif()
    if(HAVE_GETUTXENT)
        target_compile_definitions(users PUBLIC HAVE_GETUTXENT)
    endif()

    target_link_libraries(users PRIVATE libmetric)
    set_target_properties(users PROPERTIES PREFIX "")

#    add_executable(test_plugin_users EXCLUDE_FROM_ALL users_test.c ${PLUGIN_USERS_SRC})
#    target_link_libraries(test_plugin_users libtest libutils libmetric -lm)
#    add_dependencies(build_tests test_plugin_users)
#    add_test(NAME test_plugin_users COMMAND test_plugin_users WORKING_DIRECTORY ${CMAKE_SOURCE_DIR})

    install(TARGETS users DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-users.5 ncollectd-users.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-users.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
