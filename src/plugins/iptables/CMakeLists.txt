plugin_option(PLUGIN_IPTABLES "IPTables rule counters" ON)

include(CheckTypeSize)
include(CMakePushCheckState)

set(BUILD_PLUGIN_IPTABLES 0)

if(PLUGIN_IPTABLES)
    find_package(LibIptc)
    if (LIBIPTC_FOUND)
        cmake_push_check_state(RESET)
        set(CMAKE_REQUIRED_INCLUDES ${LIBIPTC_INCLUDE_DIR})
        set(CMAKE_REQUIRED_LIBRARIES ${LIBIPTC_LIBRARIES})
        set(CMAKE_EXTRA_INCLUDE_FILES libiptc/libiptc.h libiptc/libip6tc.h)
        check_type_size("iptc_handle_t" HAVE_IPTC_HANDLE_T)
        check_type_size("iptc6_handle_t" HAVE_IP6TC_HANDLE_T)
        cmake_pop_check_state()
        set(BUILD_PLUGIN_IPTABLES 1)
    else()
        set(BUILD_PLUGIN_IPTABLES_REASON "libiptc not found" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_IPTABLES ${BUILD_PLUGIN_IPTABLES} PARENT_SCOPE)

if(BUILD_PLUGIN_IPTABLES)
    set(PLUGIN_IPTABLES_SRC iptables.c)
    add_library(iptables MODULE ${PLUGIN_IPTABLES_SRC})
    if(HAVE_IPTC_HANDLE_T)
        target_compile_definitions(iptables PUBLIC HAVE_IPTC_HANDLE_T)
    endif()
    if(HAVE_IP6TC_HANDLE_T)
        target_compile_definitions(iptables PUBLIC HAVE_IP6TC_HANDLE_T)
    endif()
    target_link_libraries(iptables PRIVATE libutils libmetric LibIptc::LibIptc)
    set_target_properties(iptables PROPERTIES PREFIX "")
    install(TARGETS iptables DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-iptables.5 ncollectd-iptables.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-iptables.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
